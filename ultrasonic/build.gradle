apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'jacoco-android'
apply from: "../gradle_scripts/code_quality.gradle"

//applicationId Changed with change flavours. i dont think actual package change is needed
android {
    compileSdkVersion versions.compileSdk
    defaultConfig {
        applicationId 'pt.ipleiria.mymusicqoe'
//        versionName versions.versionName
//        versionCode versions.versionCode
        versionCode 4
        versionName "1.0.3"
        minSdkVersion versions.minSdk
        targetSdkVersion versions.targetSdk
        resConfigs "en", "pt"

    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'),
                    'minify/proguard-main.pro',
                    'minify/proguard-okhttp.pro',
                    'minify/proguard-retrofit.pro',
                    'minify/proguard-jackson.pro',
                    'minify/proguard-kotlin-reflect.pro',
                    'minify/proguard-kotlin.pro'
        }
        debug {
            minifyEnabled false
            testCoverageEnabled true
            applicationIdSuffix ".debug"
        }
    }
    sourceSets {
        main.java.srcDirs += "${projectDir}/src/main/kotlin"
        test.java.srcDirs += "${projectDir}/src/test/kotlin"
    }
    packagingOptions {
        exclude 'META-INF/LICENSE'
    }
    lintOptions {
        baselineFile file("lint-baseline.xml")
        abortOnError true
    }
    productFlavors {
    }
}

dependencies {
    implementation project(':menudrawer')
    implementation project(':pulltorefresh')
    implementation project(':library')
    implementation project(':domain')
    implementation project(':subsonic-api')
    implementation project(':cache')
    implementation androidSupport.support
    implementation androidSupport.design
    implementation other.kotlinStdlib
    testImplementation other.kotlinReflect
    testImplementation testing.junit
    testImplementation testing.kotlinJunit
    testImplementation testing.mockitoKotlin
    testImplementation testing.kluent
    implementation 'com.android.support.constraint:constraint-layout:1.1.2'
    implementation 'com.github.douglasjunior:android-simple-tooltip:0.2.2'
}

// Excluding all non-kotlin classes
ext {
    jacocoExclude = [
            '**/activity/**',
            '**/audiofx/**',
            '**/fragment/**',
            '**/provider/**',
            '**/receiver/**',
            '**/service/**',
            '**/Test/**',
            '**/util/**',
            '**/view/**',
            '**/R$*.class',
            '**/R.class',
            '**/BuildConfig.class'
    ]
}
jacocoAndroidUnitTestReport {
    excludes += jacocoExclude
}

afterEvaluate {
    testDebugUnitTest.finalizedBy jacocoTestDebugUnitTestReport
}
